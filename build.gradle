buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://maven.parchmentmc.org' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: "${forgegradle_version}", changing: false
        classpath "org.parchmentmc:librarian:${librarian_version}"
    }
}

plugins {
    id 'eclipse'
    id 'maven-publish'
    id 'checkstyle'
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'

group = 'org.shsts.tinactory'
archivesBaseName = "tinactory"
version = "${minecraft_version}-${mod_version}"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
println "Minecraft: ${minecraft_version}, Forge: ${forge_version}, ForgeGradle: ${forgegradle_version}"
minecraft {
    mappings channel: 'parchment', version: "${parchment_version}-${minecraft_version}"

    runs {
        client {
            workingDirectory project.file('run/client')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            jvmArgs '-ea'

            mods {
                tinydevices {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run/server')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            jvmArgs '-ea'

            mods {
                tinydevices {
                    source sourceSets.main
                }
            }
        }

        data {
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            jvmArgs '-ea'

            ideaModule project.name + ".test"

            mods {
                tinydevices {
                    source sourceSets.main
                }

                tinydevices_test {
                    source sourceSets.test
                }
            }

            workingDirectory project.file('run/data')
            args '--mod', 'tinactory_test', '--all', '--output', file('src/generated/resources/')
            args '--existing', file('src/test/resources/')
            args '--existing', file('src/main/resources/')
            args '--existing', file('libs/gtceu_resources_2.8.7.zip')
            args '--existing', file('libs/ae2_resources_11.7.6.zip')
        }
    }
}

repositories {
    mavenCentral()
    maven {
        // Location of the maven that hosts JEI files (and TiC)
        name = 'Progwml6 maven'
        url = 'https://dvs1.progwml6.com/files/maven'
    }
    maven {
        url = 'https://cursemaven.com'
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        name = 'shsts'
        url = 'https://www.shsts.org/m2'
        credentials {
            username = shstsUser
            password = shstsPassword
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    compileOnly fg.deobf("mezz.jei:jei-${minecraft_version}:${jei_version}:api")
    runtimeOnly fg.deobf("mezz.jei:jei-${minecraft_version}:${jei_version}")

    compileOnly files("libs/gtceu_resources_2.8.7.zip")
    compileOnly files("libs/ae2_resources_11.7.6.zip")

    runtimeOnly fg.deobf("curse.maven:ctm-267602:3933537")

    compileOnly "org.shsts.tinycorelib:core:${minecraft_version}-${tinycorelib_version}:api"
    runtimeOnly fg.deobf("org.shsts.tinycorelib:core:${minecraft_version}-${tinycorelib_version}")
    testCompileOnly "org.shsts.tinycorelib:core:${minecraft_version}-${tinycorelib_version}:api"
    testCompileOnly "org.shsts.tinycorelib:datagen:${minecraft_version}-${tinycorelib_version}:api"
    testRuntimeOnly fg.deobf("org.shsts.tinycorelib:core:${minecraft_version}-${tinycorelib_version}")
    testRuntimeOnly fg.deobf("org.shsts.tinycorelib:datagen:${minecraft_version}-${tinycorelib_version}")
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
    exclude '.cache/'
}

checkstyle {
    toolVersion = "10.20.1"
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        duplicatesStrategy = DuplicatesStrategy.INCLUDE

        include 'META-INF/mods.toml'

        expand 'mod_version': "${version}"
    }
}

processTestResources {
    from(sourceSets.test.resources.srcDirs) {
        duplicatesStrategy = DuplicatesStrategy.INCLUDE

        include 'META-INF/mods.toml'

        expand 'mod_version': "${version}"
    }
}

jar.finalizedBy('reobfJar')
